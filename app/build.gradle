apply plugin: 'com.android.application'
apply from: "$project.rootDir/tools/versions.gradle"

// Load keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.moises.mercadopagoapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode versionCodeMercadoPagoApp
        versionName versionNameMercadoPagoApp
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        android.defaultConfig.vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            try {
                keyAlias keystoreProperties['debugKeyAlias']
                keyPassword keystoreProperties['debugKeyPassword']
                storePassword keystoreProperties['debugStorePassword']
                storeFile file('../keystore/debug.keystore')
            } catch (exception) {
                throw new InvalidUserDataException('You should define debugKeyAlias, debugKeyPassword,' +
                        'and debugStorePassword in gradle.properties. ' + exception.toString())
            }
        }
        release {
            try {
                keyAlias keystoreProperties['releaseKeyAlias']
                keyPassword keystoreProperties['releaseKeyPassword']
                storePassword keystoreProperties['releaseStorePassword']
                storeFile file('../keystore/release.keystore')
            } catch (exception) {
                throw new InvalidUserDataException('You should define releaseKeyAlias, releaseKeyPassword,' +
                        'and releaseStorePassword in gradle.properties. ' + exception.toString())
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
            signingConfig signingConfigs.debug
            buildConfigField "String", "BASE_URL", keystoreProperties['urlBaseDebug']
            buildConfigField "String", "PUBLIC_KEY", keystoreProperties['publicKeyDebug']
        }

        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "BASE_URL", keystoreProperties['urlBaseRelease']
            buildConfigField "String", "PUBLIC_KEY", keystoreProperties['publicKeyRelease']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    List<String> activities = ['enterAmount','paymentMethods', 'cardIssuers', 'installments']

    sourceSets {
        main {
            activities.each { activity -> res.srcDirs "src/main/res-screen/${activity}" }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:design:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Dagger2
    implementation 'com.google.dagger:dagger-android-support:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'

    // RxJava & RxAndroid & RxBinding
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'

    // ButterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
}
